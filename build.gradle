plugins { id "com.github.johnrengelman.shadow" version "2.0.1"; id "org.sonarqube" version "2.6.1"; id "org.owasp.dependencycheck" version "3.1.2" }
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

description = "payment-method-p24"
group = 'com.payline.payment.p24'
version = '1.0-SNAPSHOT'

apply plugin: 'maven'
apply plugin: 'java-library'

configurations.all {
    // afin de récupérer la dernière snapshot à chaque fois
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

ext{
    paymentMethodVersion = '0.10-SNAPSHOT'
    mockitoVersion = '2.19.0'
}
sourceCompatibility = 1.8

task printVersion {
    doLast {
        println version
    }
}

sonarqube {
    properties {
        property "sonar.jacoco.reportPaths", "${project.buildDir}/jacoco/test.exec"
        property "sonar.dependencyCheck.reportPath", "${project.buildDir}/reports/dependency-check-report.xml"
        property 'sonar.dependencyCheck.htmlReportPath', "${project.buildDir}/reports/dependency-check-report.html"
    }
}

dependencyCheck {
    format 'ALL'
}

shadowJar {
    classifier = ''
    dependencies {
        exclude(dependency('com.payline:payment-method-api'))
        exclude(dependency('org.apache.logging.log4j:log4j-api'))
        exclude(dependency('org.apache.logging.log4j:log4j-core'))
    }
}

publishing {
    repositories {
        maven {
            if (project.version.endsWith('-SNAPSHOT')) {
                url "https://nexus.intra.monext.fr:10130/nexus/content/repositories/jindus-repo-pay-pay-snapshot/"
            } else {
                url "https://nexus.intra.monext.fr:10130/nexus/content/repositories/jindus-repo-pay-pay-public/"
            }
            credentials {
                username mavenUser
                password mavenPassword
            }
        }
    }
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}

repositories {
    mavenLocal()
    maven {
        url "https://nexus.intra.monext.fr:10130/nexus/content/repositories/jindus-repo-pay-pay-public/"
        credentials {
            username mavenUser
            password mavenPassword
        }
    }
    maven {
        url "https://nexus.intra.monext.fr:10130/nexus/content/groups/public"
        credentials {
            username mavenUser
            password mavenPassword
        }
    }
    maven {
        url "https://nexus.intra.monext.fr:10130/nexus/content/repositories/jindus-repo-pay-pay-snapshot/"
        credentials {
            username mavenUser
            password mavenPassword
        }
    }
}


dependencies {
    compileOnly("com.payline:payment-method-api:$paymentMethodVersion") {
        exclude(module: 'log4j-core')
    }
    implementation 'com.giropay:girocheckout-java:1.2.1'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
    compileOnly group: 'com.intellij', name: 'annotations', version: '12.0'
    compileOnly group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.8.2'
    testImplementation "com.payline:payment-method-api:${paymentMethodVersion}"
    //testImplementation "com.payline:payment-method-integration:${paymentMethodVersion}"
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.0.3'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: "${mockitoVersion}"
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: "${mockitoVersion}"
    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.9.0'
    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.8.2'
}

assemble.dependsOn shadowJar

jar.manifest {
    attributes('Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Built-By': System.getProperty('user.name'),
            'Built-JDK': System.getProperty('java.version'),
            'Source-Compatibility': project.sourceCompatibility,
            'Target-Compatibility': project.targetCompatibility
    )
}

test.exclude '**/*IT.class'
